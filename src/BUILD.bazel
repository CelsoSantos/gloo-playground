# -----------------------------------------------------------------------------
# Function application and library
# -----------------------------------------------------------------------------
load("@bazel_gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_push")

gazelle(name = "gazelle")

# Build function executable
go_binary(
    name = "playground_function",
    embed = [":go_default_library"],
    pure = "off",
    visibility = ["//visibility:public"],
)

# Build functionâ€™s library
go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "com.github/celsosantos/gloo-playground",
    visibility = ["//visibility:public"],
    deps = [
        "//api/proto:api_grpc_library",
        "//libs/kncloudevents:go_default_library",
        "@com_github_cloudevents_sdk_go//:go_default_library",
        "@com_github_soheilhy_cmux//:go_default_library",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//reflection:go_default_library",
        "@org_golang_x_net//context:go_default_library",
    ],
)

# -----------------------------------------------------------------------------
# Function containerization
# -----------------------------------------------------------------------------

# Generate distroless Docker image for Function
go_image(
    name = "playground_image_base",
    binary = ":playground_function",
)

# Wrap distroless Docker image into another image to expose ports to the underlying go_image
container_image(
    name = "playground_image_build",
    base = ":playground_image_base",
    ports = ["8080"],
)

# Push application Docker image to the enterprise Docker hub
#
# Pushing the image to the Docker Hub is now as simple as:
#
# $ bazel run //:mux_image_push --incompatible_disable_deprecated_attr_params=false --incompatible_disallow_dict_plus=false
#
# This command is executed in the CI/CD pipeline.
container_push(
    name = "playground_image_push",
    format = "Docker",
    image = ":playground_image_build",
    registry = "index.docker.io",
    repository = "$(DOCKER_REGISTRY_IMAGE_NAME)",
    tag = "latest",
)

# -----------------------------------------------------------------------------
# Group definitions
# -----------------------------------------------------------------------------

filegroup(
    name = "package-srcs",
    srcs = glob(["**"]),
    tags = ["automanaged"],
    visibility = ["//visibility:private"],
)

filegroup(
    name = "all-srcs",
    srcs = [":package-srcs"],
    tags = ["automanaged"],
    visibility = ["//visibility:private"],
)
